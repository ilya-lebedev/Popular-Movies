apply plugin: 'com.android.application'

def noKeyFound = '"NO_KEY_FOUND"'
def tmdbApiKey = noKeyFound

def apiKeyFile = file('../api_key.txt')
def Properties apiKeyData = new Properties()

if (apiKeyFile.canRead()) {
    apiKeyData.load(new FileInputStream(apiKeyFile))

    if (apiKeyData != null) {
        if (apiKeyData.containsKey('tmdbApiKey')) {
            tmdbApiKey = apiKeyData['tmdbApiKey']
        }
    }
}

if (tmdbApiKey == noKeyFound) {
    tmdbApiKey = '"PAST_YOUR_TMDB_API_KEY_HERE"'
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "io.github.ilya_lebedev.popularmovies"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Add TMDb API key as a constant field
        buildConfigField "String", "TMDB_API_KEY", tmdbApiKey
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
